!Made by Barnabas Piri, 2023
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/REP,FAST
!!Define constant parameters
ELAST=2.15E5
NU=0.32
forceX=100
forceY=300
A1=20
B1=50
A2=27
B2=61
L=3000
!FCR=3843.483
!!SETUP: element types, materials, sections
KEYW,PR_STRUC,1! Preferences / Structural
/PREP7
ET,1,BEAM188
KEYOPT,1,3,3
! Define the rectangular crossection
SECTYPE,1,BEAM,RECT
SECOFFSET,CENT
SECDATA,A1,B1
SECTYPE,2,BEAM,RECT
SECOFFSET,CENT
SECDATA,A2,B2
MPTEMP,1,0
MPDATA,EX,1,,ELAST
MPDATA,PRXY,1,,NU
OUTRES,ALL,ALL
! Create the keypoints
K,1,0,0,0
K,2,L/2,0,0
K,3,L,0,0
! Creating the lines
L,1,2
L,2,3
! Meshing the lines
SECNUM,2
LESIZE,1,,,10
LMESH,1
SECNUM,1
LESIZE,2,,,10
LMESH,2
! Applying the geometric constraints
DK,1,ALL,0
D,ALL,UZ,0
D,ALL,ROTX,0
FK,2,FX,-forceX
FK,3,FX,-forceX
FINISH
! Calculate prestress effect
/SOL
PSTRES,1
/STATUS,SOLU
SOLVE
FINISH
! Eigen buckling analysis
/SOL
ANTYPE,1
! Extract eight modes
BUCOPT,LANB,4,0,0,CENTER
MXPAND,4,0,0,0,0.001,
/STATUS,SOLU
SOLVE
! Post processing
/POST1
SET,FIRST
! printing the eigenshapes
*DO,III,1,4,1
  PLNSOL,U,SUM,1,1.0
  /IMAGE,SAVE,strcat(chrval(III),'_eigen_shape'),bmp
  SET,NEXT
*ENDDO
!Getting the critical value of FAST
!*GET, lam, MODE, 1, FREQ
!FCR=lam*forceX
FCR = 3843.48
!!Post buckling analysis
/SOL
! Static analysis
ANTYPE,0
! Geometric nonlinearity turned on
NLGEOM,1
NSUBST,250,250,250
NEQIT,150
OUTRES,ALL,ALL

! First substep
TIME,0
FINISH
/PREP7
FKDELE,2,ALL
FKDELE,3,ALL
FK,2,FX,0
FK,3,FX,0
FK,2,FY,0
FINISH
/SOL
LSWRITE,1

! Second substep
TIME,5
FINISH
/PREP7
FK,2,FX,0
FK,3,FX,0
FK,2,FY,forceY
FINISH
/SOL
LSWRITE,2

! Third substep
TIME,10
FINISH
/PREP7
FK,2,FX,-(9965/10000)*FCR
FK,3,FX,-(9965/10000)*FCR
FK,2,FY,forceY
FINISH
/SOL
LSWRITE,3

! Fourth substep
TIME,20
FINISH
/PREP7
FK,2,FX,-(9965/10000)*FCR
FK,3,FX,-(9965/10000)*FCR
FK,2,FY,forceY
FINISH
/SOL
LSWRITE,4

! Fourth substep
TIME,30
FINISH
/PREP7
FK,2,FX,-(9965/10000)*FCR
FK,3,FX,-(9965/10000)*FCR
FK,2,FY,0
FINISH
/SOL
LSWRITE,5

! Sixth substep
TIME,50
FINISH
/PREP7
FK,2,FX,0
FK,3,FX,0
FK,2,FY,0
FINISH
/SOL
LSWRITE,6

! Solve problem from subtep 1 to 6
LSSOLVE,1,6,1
! Time history post processing
/POST26


/POST1
! Set time stamp
APPEND,0,0,0,0,15,0,0
! Create element tables for MZ at node I and J of elements
ETABLE,MZI,SMISC,3
ETABLE,MZJ,SMISC,16
! Plot line element results (MZI and MZJ)
PLLS,MZI,MZJ,1,0,0
/IMAGE,SAVE,MZPLOT,bmp

! Create element tables for SFY at node I and J of elements
ETABLE,SFYI,SMISC,6
ETABLE,SFYJ,SMISC,19
! Plot line element results (MZI and MZJ)
PLLS,SFYI,SFYJ,1,0,0 
/IMAGE,SAVE,VYPLOT,bmp