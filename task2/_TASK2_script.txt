!Made by Barnabas Piri, 2023
/RGB,INDEX,100,100,100,0
/RGB,INDEX,0,0,0,15
/REP,FAST
!!Define constant parameters
ELAST=2.15E5
NU=0.32
F=100
A=20
B=50
L=3000
!!SETUP: element types, materials, sections
KEYW,PR_STRUC,1! Preferences / Structural
/PREP7
ET,1,BEAM188
KEYOPT,1,3,3
! Define the rectangular crossection
SECTYPE,1,BEAM,RECT
SECOFFSET,CENT
SECDATA,A,B
MPTEMP,1,0
MPDATA,EX,1,,ELAST
MPDATA,PRXY,1,,NU
! Create the keypoints
K,1,0,0,0
K,2,L/2,0,0
K,3,L,0,0
! Creating the lines
L,1,2
L,2,3
! Meshing the lines
LESIZE,1,,,2
LESIZE,2,,,2
LMESH,ALL
! Applying the geometric constraints
DK,1,ALL,0
D,ALL,UZ,0
D,ALL,ROTX,0
FK,2,FX,-F
FK,3,FX,-F
FINISH
! Calculate prestress effect
/SOL
PSTRES,1
/STATUS,SOLU
SOLVE
FINISH
! Eigen buckling analysis
/SOL
ANTYPE,1
! Extract eight modes
BUCOPT,LANB,4,0,0,CENTER
MXPAND,4,0,0,0,0.001,
/STATUS,SOLU
SOLVE
! Post processing
/POST1
SET,FIRST
! printing the eigenshapes
*DO,III,1,4,1
  PLNSOL,U,SUM,1,1.0
  /IMAGE,SAVE,strcat(chrval(III),'_eigen_shape'),bmp
  SET,NEXT
*ENDDO